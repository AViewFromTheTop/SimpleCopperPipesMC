plugins {
	id 'fabric-loom' version '1.3-SNAPSHOT'
	id 'io.github.juuxel.loom-vineflower' version '+'
	id 'maven-publish'
	id 'eclipse'
	id 'idea'
	id 'java-library'
}

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

loom {
	runtimeOnlyLog4j = true

	mixin {
		defaultRefmapName = "mixins.simple_copper_pipes.refmap.json"
	}

	accessWidenerPath = file("src/main/resources/simple_copper_pipes.accesswidener")
	interfaceInjection {
		// When enabled, injected interfaces from dependencies will be applied.
		enableDependencyInterfaceInjection = false
	}
}

configurations {
	includeModImplementation
	includeImplementation

	include.extendsFrom includeImplementation
	include.extendsFrom includeModImplementation

	implementation.extendsFrom includeImplementation
	modImplementation.extendsFrom includeModImplementation
}

repositories {
	maven {
		url = 'https://jitpack.io'
	}
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"

		content {
			includeGroup "maven.modrinth"
		}
	}
	maven {
		url = 'https://maven.terraformersmc.com'

		content {
			includeGroup "com.terraformersmc"
		}
	}
	maven {
		url = 'https://maven.shedaniel.me/'
	}
	maven {
		url 'https://cursemaven.com'

		content {
			includeGroup "curse.maven"
		}
	}
	/*maven {
		name "Siphalor's Maven"
		url "https://maven.siphalor.de"
	}*/
	maven {
		url "https://maven.flashyreese.me/releases"
	}
	maven {
		url "https://maven.flashyreese.me/snapshots"
	}
	maven {
		url = 'https://maven.minecraftforge.net/'
	}
	maven {
		url = 'https://maven.parchmentmc.org'
	}
	maven {
		url = "https://minecraft.guntram.de/maven/"
	}
	exclusiveContent {
		forRepository {
			maven {
				name = "Modrinth"
				url = "https://api.modrinth.com/maven"
			}
		}
		filter {
			includeGroup "maven.modrinth"
		}
	}
	maven {
		name = 'Quilt'
		url = 'https://maven.quiltmc.org/repository/release'
	}

	flatDir {
		dirs 'libs'
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings(loom.layered {
		// please annoy treetrain if this doesnt work
		it.officialMojangMappings {
			setNameSyntheticMembers(false)
		}
	})
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:0.2.0-rc.4"))

	modCompileOnly("maven.modrinth:create-fabric:0.5.1-d-build.1161+mc1.20.1")

	//modImplementation "de.guntram.mcmod:crowdin-translate:1.4+1.19"
	//include "de.guntram.mcmod:crowdin-translate:1.4+1.19"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modApi("maven.modrinth:frozenlib:${project.frozenlib_version}")
	include("maven.modrinth:frozenlib:${project.frozenlib_version}")

	modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
		exclude(group: "net.fabricmc.fabric-api")
		exclude(group: "com.terraformersmc")
	}

	modImplementation("maven.modrinth:modmenu:${project.modmenu_version}")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

javadoc {
	options.tags = ["reason"]
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	archiveClassifier = 'javadoc'
	from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
	archiveClassifier = 'sources'
	from sourceSets.main.allSource
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
	options.fork = true
	options.incremental = true
}

tasks.withType(Test).configureEach {
	maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17

	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {

	}
}
